<?xml version="1.0" ?>

<TranscendenceModule>

	<!-- Mission: New Freighter ================================================

	Escort a new freigher from a stargate back to Korolov. This mission is used when the Korolov
	station does not have any freighters suitable for the player to escort, or only has one or two
	freighters left.

	Uses tbPirateRaider1 and tbPirateRaider2

	EXTRA DATA

	reward:			Reward (in credits) for completing mission
	freighterID:	ID of freighter to escort
	gateID:			ID of gate where freighter will arrive
	escortType:		Type of freighter being escorted
	missionXP:		XP awarded for completing mission
	pricePerContainer:	Price per container - used to fill freighter
	cargoValue:		Value of cargo in freighter
	attackRoll:
		1	Standard pirate raid
		2	Pirates follow freighter through gate
		3	Pirates waiting at gate to ambush freighter
	playerDestroyedFreighter:	True if player destroyed the freighter (will be blacklisted)

	======================================================================== -->

	<MissionType UNID="&msKorolovNewFreighter;"
			name=			"Korolov Escort (incoming)"
			attributes=		"korolov, escort"

			level=			"1-3"

			expireTime=		"3600"
			>

		<Events>
			<OnCreate>
				(block (
					(playerLevel (korGetPlayerLevel))

					(dockedFreighters (sysFindObject aOwnerObj "sHZ O:docked; +commonwealth; +freighter;"))
					(freighterCount (count dockedFreighters))
					(qualifyCount (count
						(filter
							dockedFreighters
							theFreighter
							(and
								(geq playerLevel (objGetStaticData theFreighter "korolovMinLevel"))
								(geq (shpGetMaxSpeed gPlayerShip) (+ (shpGetMaxSpeed theFreighter) 2))
								)
							)
						))

					(validDestinations (sysFindObject aOwnerObj "TAF +populated; -occupation; -uncharted; -outlaws;"))

					gateObj escortType
					)


					(switch
						; Finite number of missions per station
						(ls (objGetData aOwnerObj 'remainingMissions) 1)
							(msnDestroy gSource)

						; Only offer to new pilots and levels 1-3
						(not (find '(0 1 2 3) playerLevel))
							(msnDestroy gSource)

						; No missions once the stronghold is destroyed
						(objIsAbandoned (objGetObjRefData aOwnerObj 'charonStronghold))
							(msnDestroy gSource)

						; If we're too close to the gates then no mission
						(not (setq gateObj (random (sysFindObject aOwnerObj "GR:300; -uncharted;"))))
							(msnDestroy gSource)

						; Only allow freighters which are slower than the player ship
						(not (setq escortType (random (filter
								(korFreighterTypes playerLevel)
								theType
								(geq (shpGetMaxSpeed gPlayerShip) (+ (typGetProperty theType 'maxSpeed) 2))
								))))
							(msnDestroy gSource)

						; If there are no freighters we're qualified to escort, or only 1-2 total, then offer mission
						(or (= qualifyCount 0) (ls freighterCount 2) (and (ls freighterCount 3) (= (random 1 3) 1)) (not validDestinations))
							(block (
								(escortData (korCalculateCargo escortType playerLevel True))
								)

								; Mission settings
								(msnSetData gSource 'gateID (objGetID gateObj))
								(msnSetData gSource 'escortType escortType)
								(msnSetData gSource 'missionXP 75)
								(msnSetData gSource 'pricePerContainer (@ escortData 'pricePerContainer))
								(msnSetData gSource 'cargoValue (@ escortData 'cargoValue))
								(msnSetData gSource 'reward (@ escortData 'escortFee))
								
								; Remember the minimum player level to escort and the minimum
								; player speed.
								(msnSetData gSource 'minPlayerLevel (typGetStaticData escortType "korolovMinLevel"))
								(msnSetData gSource 'minPlayerSpeed (+ (typGetProperty escortType 'maxSpeed) 2))
								)

						; No mission available
						(msnDestroy gSource)
						)
					)
			</OnCreate>

			<OnAccepted>
				(block (
					(gateObj (objGetObjByID (msnGetData gSource 'gateID)))
					(gateTime (sysCalcTravelTime (objGetDistance gPlayerShip gateObj) (shpGetMaxSpeed gPlayerShip)))
					(roll (random 1 100))
					)

					(switch
						; 60% of the time we use the standard Charon transport raid
						(leq roll 60)
							(msnSetData gSource 'attackRoll 1)

						; 20% of the time pirates follow the transport through the gate
						(leq roll 80)
							(msnSetData gSource 'attackRoll 2)

						; 20% of time there are pirates waiting at the gate
						(block Nil
							(msnSetData gSource 'attackRoll 3)
							(msnFireEvent gSource 'CreateGatePirates)
							)
						)
					(sysAddObjTimerEvent (add gateTime 300) gSource "OnFreighterArrives")
					)
			</OnAccepted>

			<OnDeclined>
				(msnDestroy gSource)
			</OnDeclined>

			<OnSetPlayerTarget>
				(switch
					; If we have a freighter ID, then escort it.
					(msnGetData gSource 'freighterID)
						(rpgSetTarget gSource aReason (objGetObjByID (msnGetData gSource 'freighterID)) 'escort)

					;	Otherwise, point to the stargate
					(rpgSetTarget gSource aReason (objGetObjByID (msnGetData gSource 'gateID)) 'dock)
					)
			</OnSetPlayerTarget>

			<OnCompleted>
				(korEscortMissionCompleted gSource aReason gData)
			</OnCompleted>

			<OnReward>
				(korGiveReward gSource)
			</OnReward>

			<OnObjDestroyed>
				(switch
					(= aDestroyReason 'enteredStargate)
						Nil

					; If the ship dies, mission fails
					(= (objGetID aObjDestroyed) (msnGetData gSource 'freighterID))
						(block Nil
							(msnSetData gSource 'playerDestroyedFreighter (and gPlayerShip (= aOrderGiver gPlayerShip)))
							(msnFailure gSource)
							)

					(= (objGetData aObjDestroyed 'mission) (msnGetProperty gSource 'id))
						(msnIncData gSource 'attackersLeft -1)
					)
			</OnObjDestroyed>

			<OnObjDocked>
				(switch
					; If a freighter on a mission docked, then mission is successful
					(and
						(= (objGetID aObjDocked) (msnGetData gSource 'freighterID))
						(= (objGetID aDockTarget) (msnGetProperty gSource 'ownerID))
						)
						(block Nil
							(objSetData aObjDocked "korolovPlayerEscort" Nil)
							(msnSuccess gSource)
							)
					)
			</OnObjDocked>

			<OnFreighterArrives>
				(block (
					(gateObj (objGetObjByID (msnGetData gSource 'gateID)))
					(homeBaseObj (objGetObjByID (msnGetProperty gSource 'ownerID)))
					freighterObj cargoValue
					)
					(plyMessage gPlayer "Freighter arrived!")

					(setq freighterObj (sysCreateShip (msnGetData gSource 'escortType) gateObj &svCorporate; &evKorolovFreighterBehavior;))
					(objSetData freighterObj "korolovPlayerEscort" True)
					(msnRegisterForEvents gSource freighterObj)
					(msnSetData gSource 'freighterID (objGetID freighterObj))
					(msnSetPlayerTarget gSource)

					; Load the freighter with cargo
					(korEmptyFreighter freighterObj)
					(setq cargoValue (korFillFreighter freighterObj (msnGetData gSource 'pricePerContainer)))

					; Add some attackers
					(switch
						; 60% of the time we use the standard Charon transport raid
						(= (msnGetData gSource 'attackRoll) 1)
							(block Nil
								(chrRaidTransport freighterObj homeBaseObj gateObj cargoValue)
								(objSendMessage gPlayerShip freighterObj
									(msnTranslate gSource (cat "msgBeginEscort:" (random 1 4)) {
										shipName: (objGetName freighterObj)
										})
									)
								)

						; 20% of the time pirates follow the transport through the gate
						(= (msnGetData gSource 'attackRoll) 2)
							(block Nil
								(sysAddObjTimerEvent 60 gSource 'OnPirateFollow)
								(objSendMessage gPlayerShip freighterObj
									(msnTranslate gSource (cat "msgFollowed:" (random 1 2)) {
										shipName: (objGetName freighterObj)
										})
									)
								(chrRaidTransport freighterObj homeBaseObj gateObj (/ cargoValue 2))
								)

						; 20% of time there are pirates waiting at the gate
						(block Nil
							(if (= (msnGetData gSource 'attackersLeft) 0)
								(objSendMessage gPlayerShip freighterObj
									(msnTranslate gSource (cat "msgBeginEscort:" (random 1 4)) {
										shipName: (objGetName freighterObj)
										})
									)
								)
							(if (geq (msnGetData gSource 'attackersLeft) 5)
								(objSendMessage gPlayerShip freighterObj
									(msnTranslate gSource (cat "msgAmbush:" (random 1 3)) {
										shipName: (objGetName freighterObj)
										})
									)
								)
							(msnFireEvent gSource 'OrderAttackFreighter)
							(chrRaidTransport freighterObj homeBaseObj gateObj (/ cargoValue 2))
							)
						)
					)
			</OnFreighterArrives>

			<OnPirateFollow>
				(block Nil
					(msnFireEvent gSource 'CreateGatePirates)
					(msnFireEvent gSource 'OrderAttackFreighter)
					)
			</OnPirateFollow>

			<CreateGatePirates>
				(block (
					(gateObj (objGetObjByID (msnGetData gSource 'gateID)))
					(cargoValue (msnGetData gSource 'cargoValue))
					num
					)
					(switch
						(leq cargoValue 75000)
							(setq num (random 4 6))
						(leq cargoValue 150000)
							(setq num (random 5 8))
						(leq cargoValue 300000)
							(setq num (random 6 10))
						(leq cargoValue 600000)
							(setq num (random 7 12))
						(setq num (random 8 14))
						)

					(for i 1 num (block (enemyObj)
						(setq enemyObj (sysCreateShip &tbKorolovEscortPirates; gateObj &svPirates;))
						(objSetData enemyObj 'mission (msnGetProperty gSource 'id))
						(shpOrder enemyObj 'patrol gateObj 15)
						(msnRegisterForEvents gSource enemyObj)
						(msnIncData gSource 'attackersLeft 1)
						))
					)
			</CreateGatePirates>

			<OrderAttackFreighter>
				(block ((freighterObj (objGetObjByID (msnGetData gSource 'freighterID))))
					(enum
						(filter
							(sysFindObject gSource "sD:mission;")
							obj
							(= (objGetData obj 'mission) (msnGetProperty gSource 'id))
							)
						enemyObj
						(block Nil
							(shpCancelOrders enemyObj)
							(shpOrder enemyObj 'attack freighterObj)
							)
						)
					)
			</OrderAttackFreighter>
		</Events>

		<Language>
			<Text id="Name">
				Escort Korolov Freighter
			</Text>

			<Text id="Summary">
				(msnTranslate gSource 'textSummary {
					gateName: (objGetName (objGetObjByID (msnGetData gSource 'gateID)) 'article)
					systemName: (sysGetName)
					payment: (fmtCurrency 'credit (msnGetData gSource 'reward))
					})
			</Text>
			<Text id="textSummary">
				Your mission is to escort a freighter arriving at the %gateName%
				back to the Korolov station and defend it from any attackers.

				System: %systemName%\n
				Payment: %payment%
			</Text>
			<Text id="listSummary">
				(block (
					(gate (objGetObjByID (msnGetData gSource 'gateID)))
					(destName (objGetName gate 'article))
					(paymentValue (msnGetData gSource 'reward))
					(payment 
						(if (= paymentValue 0)
							(msnTranslate gSource 'listSummary.noPayment)
							(fmtCurrency 'credit paymentValue)
							)
						)
					)
					(msnTranslate gSource 'listSummary.fromGate { destName:destName payment:payment })
					)
			</Text>
			<Text id="listSummary.fromGate">
				From %destName% to Korolov Shipping\n
				Payment: %payment%
			</Text>

			<Text id="Briefing">
				(msnTranslate gSource 'textBriefing {
					shipClass: (typGetDataField (msnGetData gSource 'escortType) 'name)
					gateName: (objGetName (objGetObjByID (msnGetData gSource 'gateID)) 'article)
					payment: (fmtCurrency 'credit (msnGetData gSource 'reward))
					})
			</Text>
			<Text id="textBriefing">
				A %shipClass% will be entering the system at %gateName%.
				We will pay %payment% if you escort her safely back to our location.

				Do you wish to accept this assignment?
			</Text>

			<String id="AcceptReply">
				"Great! The freighter will be arriving in system shortly so don't delay. Good luck!"
			</String>

			<String id="DeclineReply">
				"Oh well"
			</String>

			<String id="InProgress">
				"The freighter you're supposed to be escorting is already on its way!"
			</String>

			<Text id="FailureDebrief">
				(if (msnGetData gSource 'playerDestroyedFreighter)
					(msnTranslate gSource "textFailureDebrief:blacklist")
					(msnTranslate gSource "textFailureDebrief:default")
					)
			</Text>
			<Text id="textFailureDebrief:blacklist">
				"Either your poor marksmanship or deliberate malice destroyed the
				freighter entrusted to your care! You will never work for us again, captain."
			</Text>
			<Text id="textFailureDebrief:default">
				"Your timidity under fire and your lack of skill doomed the poor crew
				of your freighter and its cargo!"
			</Text>

			<Text id="SuccessDebrief">
				(korSuccessDebrief gSource)
			</Text>

			<Text id="msgBeginEscort:1">Glad to have you with us, captain</Text>
			<Text id="msgBeginEscort:2">%ShipName% reporting in.</Text>
			<Text id="msgBeginEscort:3">Good to have you on our wing, %name%</Text>
			<Text id="msgBeginEscort:4">%ShipName% on course.</Text>

			<Text id="msgFollowed:1">Help! The pirates are right behind us</Text>
			<Text id="msgFollowed:2">Mayday mayday mayday. %ShipName% under attack by pirates.</Text>

			<Text id="msgAmbush:1">Kack! The pirates were waiting for us.</Text>
			<Text id="msgAmbush:2">It's an ambush!</Text>
			<Text id="msgAmbush:3">It's a kacking warzone out here!</Text>
		</Language>

	</MissionType>

<!-- TABLES -->

	<ShipTable unid="&tbKorolovEscortPirates;">
		<LevelTable>
			<Ship levelFrequency="cccur r----" class="&scCorsair;" />
			<Ship levelFrequency="vuccc urr--" class="&scViking;" />
			<Ship levelFrequency="-vucc currr" class="&scCorsair-II;" />
			<Ship levelFrequency="--vuc ccurr" class="&scViking-II;" />
			<Ship levelFrequency="---vu cccuu" class="&scBarbary;" />
		</LevelTable>
	</ShipTable>

</TranscendenceModule>
