<?xml version="1.0" encoding="utf-8"?>

<TranscendenceModule>
	
<!-- ANALYZE SCREEN ============================================================
	
	GLOBAL DATA
	
	skillLevel:			Total number of successful uses
	
	ITEM OBJECT
	
	We fire OnAnalyzeItem to allow the item to handle analysis. The item should
	return a struct with the following fields:
	
		desc: A description of the result of the analysis.
		modifiedItem: The modified item.
	
-->

	<DockScreen UNID="&dsRPGAnalyzeItem;"
			inherit=			"&dsDockScreenBase;"
			>

		<OnScreenInit>
			(scrSetData gScreen 'analyzer gItem)
		</OnScreenInit>

		<Display type="customItemPicker">
			<OnDisplayInit>
				(filter (objGetItems gSource "*") theItem
					(or
						(not (itmIsKnown theItem))
						(objFireItemEvent gSource theItem 'GetIsAnalysisRequired)
						)
					)
			</OnDisplayInit>
		</Display>

		<Panes>
			<Default>
				<OnPaneInit>
					(block (
						(analyzerItem (scrGetData gScreen 'analyzer))
						(skillLevel (typGetData &dsRPGAnalyzeItem; 'skillLevel))
						(chargesLeft (itmGetProperty analyzerItem 'charges))
						(itemToAnalyze (scrGetItem gScreen))
						canAnalyze result
						)
						
						;	Init skill level
						(if (not skillLevel)
							(block Nil
								(typSetData &dsRPGAnalyzeItem; 'skillLevel 0)
								(setq skillLevel 0)
								)
							)
							
						;	Report the number of charges remaining
						(scrSetControlValue gScreen 'chargesLeft
							(scrTranslate gScreen 'descChargesLeft { charges:chargesLeft })
							)
						
						;	Continue with description
						
						(setq canAnalyze Nil)
						(switch
							;	Have we already analyzed an item
							(setq result (scrGetData gScreen 'result))
								(scrSetDesc gScreen result)

							;	If no more charges then we cannot analyze anything
							
							(eq chargesLeft 0)
								(scrSetDescTranslate gScreen 'descNoCharges { name:(itmGetName analyzerItem 0x40) })
								
							;	Nothing to analyze
							
							(not itemToAnalyze)
								(scrSetDescTranslate gScreen 'descNoItemsHere)
								
							;	Check to see if this analyzer can handle this item
							
							(and (setq result (objFireItemEvent gSource analyzerItem 'CanAnalyzeItem { itemToAnalyze: itemToAnalyze }))
									(not (@ result 'canAnalyzeItem))
									)
								(scrSetDesc gScreen (@ result 'message))
								
							;	OK
							
							(block Nil
								(switch
									(ls skillLevel 2)
										(scrSetDescTranslate gScreen 'descAnalyze1 { name:(itmGetName itemToAnalyze 0x804) })
									
									(ls skillLevel 5)
										(scrSetDescTranslate gScreen 'descAnalyze2 { name:(itmGetName itemToAnalyze 0x804) })
									
									(ls skillLevel 10)
										(scrSetDescTranslate gScreen 'descAnalyze3 { name:(itmGetName itemToAnalyze 0x804) })
									
									(scrSetDescTranslate gScreen 'descAnalyze4 { name:(itmGetName itemToAnalyze 0x804) })
									)
								
								(setq canAnalyze True)
								)
							)
						
						;	Initialize actions

						(if (scrGetData gScreen 'result)
							(block Nil
								(scrShowAction gScreen 'actionAnalyze Nil)
								(scrShowAction gScreen 'actionCancel Nil)
								(scrShowAction gScreen 'actionContinue True)
								(scrShowAction gScreen 'actionDone True)
								(scrEnableAction gScreen 'actionContinue (gr chargesLeft 0))
								)
							(block Nil
								(scrShowAction gScreen 'actionAnalyze True)
								(scrShowAction gScreen 'actionCancel True)
								(scrShowAction gScreen 'actionContinue Nil)
								(scrShowAction gScreen 'actionDone Nil)
								(scrEnableAction gScreen 'actionAnalyze canAnalyze)
								)
							)
						)
				</OnPaneInit>

				<Controls>
					<Text id="chargesLeft"/>
				</Controls>

				<Actions>
					<Action id="actionAnalyze" default="1">
						(block (
							(analyzerItem (scrGetData gScreen 'analyzer))
							(result (rpgAnalyzeItem gSource (scrGetItem gScreen)))
							)
						
							;	Consume a charge on the analyzer
							(setq analyzerItem (objSetItemProperty gSource analyzerItem 'incCharges -1))
							(scrSetData gScreen 'analyzer analyzerItem)
							
							;	Save the result
							(scrSetData gScreen 'result (@ result 'desc))
							
							;	Set the filter so we only select the modified item
							(scrSetListFilter gScreen (lambda (theItem)
								(block Nil
									;(dbgOutput "item1: " (itmGetName theItem 0))
									;(dbgOutput "item2: " (itmGetName (@ result 'modifiedItem) 0))
									(itmIsEqual theItem (@ result 'modifiedItem))
									)
								))
							(scrShowPane gScreen 'Default)
							)
					</Action>

					<Action id="actionCancel" cancel="1">
						(scrExitScreen gScreen)
					</Action>

					<Action id="actionContinue" default="1">
						(block Nil
							;	Clear last result
							(scrSetData gScreen 'result Nil)

							;	Reset filter to show all items again
							(scrSetListFilter gScreen (lambda (theItem)
								(or
									(not (itmIsKnown theItem))
									(objFireItemEvent gSource theItem 'GetIsAnalysisRequired)
									)
								))

							(scrShowPane gScreen 'Default)
							)
					</Action>

					<Action id="actionDone" cancel="1">
						(scrExitScreen gScreen)
					</Action>
				</Actions>
			</Default>
		</Panes>

		<Language>
			<Text id="actionAnalyze">[A]nalyze Item</Text>

			<Text id="descChargesLeft">Charges Left: %charges%</Text>
			<Text id="descNoCharges">
				Unfortunately, %name% is out of charge and cannot function.
			</Text>
			<Text id="descNoItemsHere">
				There are no items here requiring analysis.
			</Text>
			<Text id="descAnalyze1">
				You can analyze %name%, though you do not have much experience analyzing items.
			</Text>
			<Text id="descAnalyze2">
				You can analyze %name%; you have some experience analyzing items.
			</Text>
			<Text id="descAnalyze3">
				You can analyze %name%; you are proficient at analyzing items.
			</Text>
			<Text id="descAnalyze4">
				You can analyze %name%; you are an expert at analyzing items.
			</Text>
		</Language>
	</DockScreen>

<!-- GLOBAL FUNCTIONS ==========================================================

-->

	<Globals>
		(block Nil
			(setq rpgAnalyzeItem (lambda (sourceObj itemToAnalyze desc modifiedItem)
			
				;	We return a struct with the following elements:
				;
				;		desc: The description of the result.
				
				(block (skillLevel failureChance result)
					
					;	Compute the chance of failure based on skillLevel
					
					(setq skillLevel (typGetData &dsRPGAnalyzeItem; 'skillLevel))
					(switch
						(ls skillLevel 2)
							(setq failureChance 20)

						(ls skillLevel 5)
							(setq failureChance 10)

						(ls skillLevel 10)
							(setq failureChance 5)

						(setq failureChance 0)
						)
						
					;	Our skill level increases
					
					(typIncData &dsRPGAnalyzeItem; 'skillLevel 1)
						
					;	Analyze
					
					(switch
						;	There is a chance of failure, depending on the skill of
						;	the user.
						
						(leq (random 1 100) failureChance)
							(block Nil
							
								(switch
								
									;	Analysis failed, but the original item is intact
									
									(gr (random 1 100) (divide failureChance 2))
										(setq desc (cat "Unfortunately, you botched the analysis and got no results."))
										
									;	If this is an undamaged device then it got damaged by
									;	the analysis.
									
									(itmMatches itemToAnalyze "dN")
										(block Nil
											(setq desc (cat
												"You attempt to analyze " (itmGetName itemToAnalyze 0x40) " but accidentally short out a major component. "
												"The " (itmGetName itemToAnalyze 0x80) " has been damaged."
												))
											(setq modifiedItem (objSetItemProperty sourceObj itemToAnalyze 'damaged True))
											)
											
									;	If this is an item that can be destroyed, it is.
									
									(itmMatches itemToAnalyze "ut~ad -Res;")
										(block Nil
											(setq desc (cat
												"You attempt to analyze " (itmGetName itemToAnalyze 0x40) " but accidentally destroy " 
												(if (gr (itmGetCount itemToAnalyze) 1) "them" "it")
												" with a burst of high-energy ions. "
												))
											(objRemoveItem sourceObj itemToAnalyze)
											(setq modifiedItem Nil)
											)
									
									;	Otherwise, failure.
									
									(block Nil
										(setq desc (cat "Unfortunately, you botched the analysis and got no results."))
										(setq modifiedItem itemToAnalyze)
										)
									)
								)
					
						;	First we let the item analyze itself. If it works, then
						;	we return the result.
						
						(setq result (objFireItemEvent sourceObj itemToAnalyze 'OnAnalyzeItem))
							(block Nil
								(setq desc (@ result 'desc))
								(setq modifiedItem (@ result 'modifiedItem))
								)
						
						;	If this is an unknown item, then we identify it.
						
						(not (itmIsKnown itemToAnalyze))
							(block Nil
								(itmSetKnown itemToAnalyze)
								(setq desc (cat
									"Your analysis reveals it to be " (itmGetName itemToAnalyze 0x04) "."
									))
								(setq modifiedItem itemToAnalyze)
								)
								
						;	Otherwise, nothing
						
						(block Nil
							(setq desc (cat
								"You run the analyzer but are unable to reveal any additional information on the item."
								))
							(setq modifiedItem itemToAnalyze)
							)
						)
						
					;	Done
					
					{
						desc: desc
						modifiedItem: itemToAnalyze
						}
					)
				))
			)
	</Globals>

</TranscendenceModule>
