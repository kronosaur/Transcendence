<?xml version="1.0" encoding="utf-8"?>

<TranscendenceModule>
	
	<Sovereign UNID="&svFriendlyAuton;"
			name="autons"
			demonym=":an auton(s)"

			alignment="constructive chaos"
			>
	</Sovereign>

	<!-- Auton Base Class
	
	Do not rely on the implementation of this class across versions. If you wish
	to inherit from this class in an Extension, you should copy the class into
	your own Extension and then inherit from that.

	EXTRA DATA
	
	autonItemType:		The type of item that created the auton.
	
	behavior:			Autons's current behavior
							Nil						= nothing
							'attackingAtWill		= attacking any target in range
							'attackingTarget		= attacking a target
							'custom					= handled by derived class
							'escorting				= escorting the player
							'returning				= returning to player ship
							'waiting				= waiting for the player

	EVENTS
		
	Derived types may implement the following events:
		
	CreateFromItem: Create the auton ship. gData has the following fields:
				
			item: The item used to create the auton
			sourceObj: The object launching the auton
			type: The ship class of the auton to create
				
			By default this event creates an auton of the given type and 
			orders it to escort the launching object.
				
			NOTE: If you override this event it recommended that you call
			the base class event (which returns the auton object) and then
			modify the auton.
				
	ConvertToItem: This event must return an item structure representing the
			auton when converted to an item. By default we look at the auton
			object's data ("autonItemType"), which is set by the default
			CreateFromItem event.
		
	OrderAttackSelf: Implement behavior when the auton is ordered to attack 
			itself. By default, the auton refuses to comply.
			
	LANGUAGE
	
	Derrived types may define the following language elements:
	
	AttackTargetAck: Reply when auton is ordered to attack a valid target.
	
	DeployAck: Reply when auton is deployed.
	
	FormUpAck: Reply when auton is ordered to form up.
	
	ReturnAck: Reply when auton is ordered to return to the ship.
	
	WaitAck: Reply when auton is ordered to wait.
				
	-->

	<Type UNID="&baStdAutonBase;">
		<Communications>
			<Message id="msgAttackTarget" name="Attack target" key="A">
				<OnShow>
					(and (objGetTarget gSender)
						(not (eq (objGetTarget gSender) (objGetTarget gSource)))
						(not (shpGetAISetting gSource 'nonCombatant))
						(objGetItems gSource "wI")
						(objGetProperty gSource 'playerWingman)
						)
				</OnShow>
				
				<Invoke>
					(switch
						;	If ordered to attack ourselves, hilarity ensues
						
						(eq (objGetTarget gSender) gSource)
							(objFireEvent gSource "OrderAttackSelf")
							
						;	If no target, unable to compy
						
						(not (objGetTarget gSender))
							(objSendMessage gSender gSource (objTranslate gSource 'UnableToComply))

						;	Attack the target
						
						(block Nil
							(shpCancelOrders gSource)
							(shpOrder gSource 'attack (objGetTarget gSender))
							
							(objSetData gSource 'behavior 'attackingTarget)
							(objSendMessage gSender gSource (objTranslate gSource 'AttackTargetAck))
							)
						)
				</Invoke>
			</Message>

			<Message id="msgBreakAndAttack" name="Break &amp; attack" key="B">
				<OnShow>
					(and (neq (objGetData gSource 'behavior) 'attackingAtWill)
						(not (shpGetAISetting gSource 'nonCombatant))
						(objGetItems gSource "wI")
						(objGetProperty gSource 'playerWingman)
						)
				</OnShow>
				
				<Invoke>
					(block (theTarget)
					
						;	Get the closest angry target to the sender that the sender can detect.
						;	Store the result so we only have to check once each tick.
						(if (eq (unvGetTick) (objGetData gSender 'closestTargetCheckTick))
						
							(setq theTarget (objGetData gSender 'closestBreakAndAttackTarget))
							
							(block nil
								(setq theTarget (sysFindObject gSender "sTYAPN N:100;"))
								(objSetData gSender 'closestBreakAndAttackTarget theTarget)
								(objSetData gSender 'closestTargetCheckTick (unvGetTick))
								)
							)
							
						(switch
						
							;	Attack the closest target to the sender if we have one.
							theTarget
								(block nil
									(shpCancelOrders gSource)
									(shpOrder gSource 'attack theTarget)
									(objSetData gSource 'behavior 'attackingAtWill)
									(objSendMessage gSender gSource (objTranslate gSource 'AttackTargetAck))
									)
							
							;	If we're already attacking something, don't change target, but switch behavior.
							(eq (objGetData gSource 'behavior) 'attackingTarget)
								(block nil
									(objSetData gSource 'behavior 'attackingAtWill)
									(objSendMessage gSender gSource (objTranslate gSource 'AttackTargetAck))
									)
									
							;	Attack the closest target to ourself that we can detect, if any.
							(setq theTarget (sysFindObject gSource "sTYAPN N:100;"))
								(block nil
									(shpCancelOrders gSource)
									(shpOrder gSource 'attack theTarget)
									(objSetData gSource 'behavior 'attackingAtWill)
									(objSendMessage gSender gSource (objTranslate gSource 'AttackTargetAck))
									)
									
							;	Nothing to attack.	
							(objSendMessage gSender gSource (objTranslate gSource 'NoTargetInRange))
							)
						)
				</Invoke>
			</Message>

			<Message id="msgFormUp" name="Form up" key="F">
				<OnShow>
					(and (neq (objGetData gSource 'behavior) 'escorting)
						(objGetProperty gSource 'playerWingman)
						)
				</OnShow>

				<Invoke>
					(block (behavior)
						(setq behavior (objGetData gSource 'behavior))
						(switch
							(or (not behavior) (eq behavior 'escorting))
								(block Nil
									(objCommunicate gSource gSender 'FormUp)
									(objSendMessage gSender gSource (objTranslate gSource 'FormUpAck))
									)

							(block Nil
								(shpCancelOrders gSource)
								(shpOrder gSource 'escort gPlayerShip)
								
								(objSetData gSource 'behavior 'escorting)
								(objSendMessage gSender gSource (objTranslate gSource 'FormUpAck))
								)
							)
						)
				</Invoke>
			</Message>
			
			<Message id="msgReturnToShip" name="Return to ship" key="R">
				<OnShow>
					(and
						(objGetItems gSender "*IN +AutonBay;")
						(objGetData gSource 'autonItemType)
						(neq (objGetData gSource 'behavior) 'returning)
						(objGetProperty gSource 'playerWingman)
						)
				</OnShow>
				
				<Invoke>
					(block (spaceLeft mass)
					
						;	Make sure there will be enough space to return, accounting for
						;	autons that are already returning. Search the system if we haven't
						;	done so yet this tick.
						
						(if (eq (unvGetTick) (objGetData gSender 'returningAutonMassCheckTick))
							(setq spaceLeft (objGetData gSender 'spaceLeftAfterAutonsReturn))
							(block nil
								(setq spaceLeft (objGetCargoSpaceLeft gSender))
								(enum (sysFindObject nil 's) theShip
									(if (and (objGetData theShip 'autonItemType)
											(eq (objGetData theShip 'behavior) 'returning)
											(eq (shpGetOrderTarget theShip) gSender)
											)
										(setq spaceLeft (- spaceLeft (objFireEvent theShip "CalcMass")))
										)
									)
								(objSetData gSender 'returningAutonMassCheckTick (unvGetTick))
								)
							)
						
						(setq mass (objFireEvent gSource "CalcMass"))
						(if (gr mass spaceLeft)
						
							;	Not enough space
							(objSendMessage gSender gSource (objTranslate gSource 'ErrNoSpaceToReturn))
								
							;	Return
							(block Nil
								(shpCancelOrders gSource)
								(shpOrder gSource 'gate gSender)
							
								(objSetData gSource 'behavior 'returning)
								(objSendMessage gSender gSource (objTranslate gSource 'ReturnAck))
								
								(setq spaceLeft (- spaceLeft mass))
								)
							)
							
						;	Remember remaining space
						(objSetData gSender 'spaceLeftAfterAutonsReturn spaceLeft)
						)
				</Invoke>
			</Message>

			<Message id="msgWait" name="Wait" key="W">
				<OnShow>
					(and (neq (objGetData gSource 'behavior) 'waiting)
						(objGetProperty gSource 'playerWingman)
						)
				</OnShow>

				<Invoke>
					(block Nil
						(shpCancelOrders gSource)
						(shpOrder gSource 'hold)
						
						(objSetData gSource 'behavior 'waiting)
						(objSendMessage gSender gSource (objTranslate gSource 'WaitAck))
						)
				</Invoke>
			</Message>
		</Communications>
		
		<Events>
			<CalcMass>
				(block (autonType)
					(setq autonType (objGetData gSource 'autonItemType))
					(if (not autonType)
						(setq autonType &itAutoSentinel;)
						)

					(add
						(itmGetMass autonType)
						(map (objGetItems gSource "*U") 'reduceSum theItem 
							(multiply (itmGetCount theItem) (itmGetMass theItem))
							)
						)
					)
			</CalcMass>
			
			<ConvertToItem>
				(block (autonType autonItem)
					(setq autonType (objGetData gSource 'autonItemType))
					(setq autonItem 
						(itmCreate (if autonType autonType &itAutoSentinel;) 1)
						)
						
					;	Store the current armor and devices on the auton
					
					(setq autonItem (itmSetData autonItem 'autonConfig {
						armor: (map (objGetItems gSource "aI") theArmor	{
							item: (itmSetProperty theArmor 'installed Nil)
							hp: (subtract (itmGetProperty theArmor 'completeHP) (objGetArmorDamage gSource theArmor))
							})
							
						devices: (map (objGetItems gSource "dI") theDev {
							item: (itmSetProperty theDev 'installed Nil)
							})
							
						cargo: (objGetItems gSource "*U")
						
						name: (if (neq (objGetName gSource) (objGetProperty gSource 'name))
									(objGetName gSource)
									)
						}))
						
					;	Add one charge on the item for every ton of cargo inside the auton
					
					(setq autonItem (itmSetProperty autonItem 'charges
						(divide 
							(map (objGetItems gSource "*U") 'reduceSum theItem 
								(multiply (itmGetCount theItem) (itmGetMass theItem))
								)
							1000
							)
						))
						
					;	Done
					
					autonItem
					)
			</ConvertToItem>
			
			<CreateFromItem>
				(block (auton sourceObj sourceItem autonConfig)
					(setq sourceObj (@ gData 'sourceObj))
					(setq sourceItem (@ gData 'item))
					
					;	Create the auton
					
					(setq auton
						(sysCreateShip 
							(@ gData 'type)
							(objGetPos sourceObj)
							&svFriendlyAuton;
							)
						)
						
					;	If the source item has a configuration then we need to initialize the ship.
					
					(if (setq autonConfig (itmGetData sourceItem 'autonConfig))
						(block Nil
						
							;	Start by installing the proper armor
							
							(for i 0 (subtract (shpGetArmorCount auton) 1)
								(block (newArmorItem)
									(setq newArmorItem (@ (@ (@ autonConfig 'armor) i) 'item))
									
									(objAddItem auton newArmorItem)
									(shpInstallArmor auton newArmorItem i)
									
									;	Set the appropriate hit points
									
									(objSetItemProperty auton (shpGetArmor auton i) 'hp (@ (@ (@ autonConfig 'armor) i) 'hp))
									)
								)
								
							;	Uninstall all devices
							
							(enum (objGetItems auton "dI") theItem
								(shpRemoveDevice auton theItem)
								)
								
							;	Install required devices
							
							(enum (@ autonConfig 'devices) deviceDesc
								(block (newDeviceItem)
									(setq newDeviceItem (@ deviceDesc 'item))
									
									(objAddItem auton newDeviceItem)
									(shpInstallDevice auton newDeviceItem)
									)
								)
								
							;	Remove uninstalled armor and devices
								
							(enum (objGetItems auton "adU") theItem
								(objRemoveItem auton theItem)
								)
								
							;	Add items to cargo
							
							(enum (@ autonConfig 'cargo) theItem
								(objAddItem auton theItem)
								)
							
							;	Set name
							
							(if (@ autonConfig 'name)
								(objSetName auton (@ autonConfig 'name))
								)
								
							)
						)
						
					;	Order the auton to escort the source
					
					(shpOrder auton 'escort sourceObj)
					(objSetData auton 'behavior 'escorting)
					
					;	Store the item type in the auton object so that we can
					;	convert it back later.
					
					(objSetData auton 'autonItem sourceItem)
					(objSetData auton 'autonItemType (itmGetType sourceItem))
					
					;	Welcome or error message
					
					(if (@ gData 'notEnoughSpace)
						(objSendMessage sourceObj auton (objTranslate auton 'ErrNoSpaceToReturn))
						(objSendMessage sourceObj auton (objTranslate auton 'DeployAck))
						)
					
					;	Return the auton created, in case subclasses want to
					;	modify it.
					
					auton
					)
			</CreateFromItem>
			
			<OnCreate>
				(block Nil
					;	If this is a friendly auton then we assume that we are escorting
					;	the player. We need to do this for backwards compatibility, in case
					;	anyone tries to create an auton without going through CreateFromItem
					
					(if (eq (objGetSovereign gSource) &svFriendlyAuton;)
						(block Nil
							(objSetProperty gSource 'playerWingman True)
							)
						)
					)
			</OnCreate>
			
			<OnEnteredGate>
				;	If we returned to a ship or non-gate station, convert back to an item
				
				(if (objMatches aGateObj nil "st -stargate")
					(block ((autonItem (objFireEvent gSource "ConvertToItem"))
							(autonShipClass (itmGetStaticData autonItem 'autonShipClass))
							)
						(if (gr (itmGetMass autonItem) (objGetCargoSpaceLeft aGateObj))
						
							; If there's not enough space, redeploy with an error message
							(typFireEvent autonShipClass 'CreateFromItem
								{ sourceObj:aGateObj type:autonShipClass item:autonItem notEnoughSpace:true }
								)
							
							; Otherwise, add to cargo
							(objAddItem aGateObj autonItem)
							)
						)
					)
			</OnEnteredGate>
			
			<OnOrdersCompleted>
				(block (theTarget)
					(switch
						(not (objGetProperty gSource 'playerWingman))
							Nil
						
						(not gPlayerShip)
							(block Nil
								(shpOrder gSource 'hold)
								(objSetData gSource 'behavior 'waiting)
								)
							
						(eq (objGetData gSource 'behavior) 'attackingAtWill)
							(block nil
								;	Get the closest angry target to the player that they can detect.
								;	Store the result so we only have to check once each tick.
								(if (eq (unvGetTick) (objGetData gPlayerShip 'closestTargetCheckTick))
						
									(setq theTarget (objGetData gPlayerShip 'closestBreakAndAttackTarget))
									
									(block nil
										(setq theTarget (sysFindObject gPlayerShip "sTYAPN N:100;"))
										(objSetData gPlayerShip 'closestBreakAndAttackTarget theTarget)
										(objSetData gPlayerShip 'closestTargetCheckTick (unvGetTick))
										)
									)
								
								(switch
						
									;	Attack the closest target to the player if we have one.
									theTarget
										(shpOrder gSource 'attack theTarget)
											
									;	Attack the closest target to ourself that we can detect, if any.
									(setq theTarget (sysFindObject gSource "sTYAPN N:100;"))
										(shpOrder gSource 'attack theTarget)
										
									;	Nothing to attack, return to formation.
									(block Nil
										(shpOrder gSource 'escort gPlayerShip)
										(objSetData gSource 'behavior 'escorting)
										)
									)
								)
								
						;	Default: return to formation.
						(block Nil
							(shpOrder gSource 'escort gPlayerShip)
							(objSetData gSource 'behavior 'escorting)
							)
							
						)
					)
			</OnOrdersCompleted>
			
			<OnPlayerLeftSystem>
				(block (behavior)
					(setq behavior (objGetData gSource 'behavior))
					(switch
						(not (objGetProperty gSource 'playerWingman))
							Nil
							
						; If we're waiting, stay in this system and wait for the player
						
						(eq behavior 'waiting)
							'waitForPlayer

						; Otherwise, follow the player through the gate
						
						'followPlayer
						)
					)
			</OnPlayerLeftSystem>
			
			<OrderAttackSelf>
				(objSendMessage gSender gSource (objTranslate gSource 'UnableToComply))
			</OrderAttackSelf>
		</Events>
		
		<Language>
			<String id="WatchYourTargets"></String>
			<String id="NiceShooting"></String>
			<String id="UnableToComply">"Unable to comply"</String>
			<String id="AttackTargetAck">"Auton order acknowledged"</String>
			<String id="NoTargetInRange">"No target in range"</String>
			<String id="FormUpAck">"Auton order acknowledged"</String>
			<String id="ErrNoSpaceToReturn">"Not enough space in cargo hold to return to ship"</String>
			<String id="ReturnAck">"Auton order acknowledged"</String>
			<String id="WaitAck">"Auton order acknowledged"</String>
			<Text id="DeployAck">(objTranslate gSource 'DeployAckString { autonName:(objGetName auton 0x01) } )</Text>
			<String id="DeployAckString">"%autonName% online"</String>
		</Language>
	</Type>

<!-- GLOBAL FUNCTIONS ==========================================================

-->

	<Globals>
		(block Nil
			(setq rpgCleanUpSuspendedAuton (lambda (sourceObj)
				(block (auton)
					(if (setq auton (objGetObjRefData sourceObj 'rpgSuspendedAuton))
						(objDestroy auton)
						)
						
					(objSetObjRefData sourceObj 'rpgSuspendedAuton Nil)
					)
				))

			(setq rpgCreateSuspendedAuton (lambda (sourceObj autonItem)
				(block (autonUNID auton)
					(switch
					
						;	Get the UNID
						
						(not (setq autonUNID (itmGetStaticData autonItem 'autonShipClass)))
							Nil
					
						;	Check to see if we've already created this auton. If so, then
						;	we just return it.
					
						(and (setq auton (objGetObjRefData sourceObj 'rpgSuspendedAuton))
								(itmIsEqual autonItem (objGetData auton 'autonItem))
								)
							auton
					
						;	Let the auton ship class create itself based on the item
					
						(block Nil
							(setq auton
								(typFireEvent autonUNID 'CreateFromItem { sourceObj:sourceObj type:autonUNID item:autonItem })
								)
								
							;	Suspend the ship
							
							(objSuspend auton)
								
							;	Remember the suspended auton, in case we need it later.
							
							(objSetObjRefData sourceObj 'rpgSuspendedAuton auton)
							
							;	Done
							
							auton
							)
						)
					)
				))
				
			(setq rpgDeployAutonFromItem (lambda (sourceObj autonUNID autonItem)
				(block Nil
					;	Let the auton ship class create itself based on the item
				
					(typFireEvent autonUNID 'CreateFromItem { sourceObj:sourceObj type:autonUNID item:autonItem })
				
					;	Identify and remove the item from the source
				
					(itmSetKnown autonItem)
					(objRemoveItem sourceObj autonItem 1)
					)
				))
				
			(setq rpgGetAutonConfig (lambda (autonItem)
				;
				;	We return a struct with the following elements:
				;
				;		armor: An array of structs, each struct with the following elements:
				;
				;				item: An item descriptor
				;				hp: The number of hit points left
				;
				;		default: If True, then the auton is identical to its original
				;				ship class.
				;
				;		devices: An array of structs, each struct with the following
				;				elements:
				;
				;				item: An item descriptor
				
				(block (savedConfig autonShipClass)
				
					(switch
						;	We've already stored the information, so we just return it.
						
						(setq savedConfig (itmGetData autonItem 'autonConfig))
							savedConfig
							
						;	If we have the ship class then we can take information directly
						;	from it.
						
						(setq autonShipClass (itmGetStaticData autonItem 'autonShipClass))
							{	default: True
							
								armor: (map (typGetDataField autonShipClass 'armorItems) Nil theArmor
									{
										item: theArmor
										hp: (itmGetProperty theArmor 'completeHP)
										}
									)

								devices: (map (typGetDataField autonShipClass 'deviceItems) Nil theDevice
									{
										item: theDevice
										}
									)
								}

						;	Otherwise, there is nothing we can do.

						Nil
						)
					)
				))
			)
	</Globals>

</TranscendenceModule>
