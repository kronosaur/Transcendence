<?xml version="1.0" encoding="utf-8"?>

<TranscendenceModule>
	
	<Sovereign UNID="&svFriendlyAuton;"
			name="autons"
			demonym=":an auton(s)"

			alignment="constructive chaos"
			>
	</Sovereign>

	<ItemType UNID="&baAutonItemBase;"
		name=		"(auton item base)"
		virtual=	"true"
		>
		
		<Invoke key="A">
			(rpgDeployAutonFromItem gSource (itmGetStaticData gItem 'autonShipClass) gItem)
		</Invoke>
		
		<Events>
			<GetName>
				(block (name singularClass pluralClass)
					(if (find aFlags 'actual)
						(block Nil
							(setq singularClass (itmGetName gItem '(noEvent noModifiers actual)))
							(setq pluralClass (itmGetName gItem '(noEvent noModifiers actual plural)))
							)
						(block Nil
							(setq singularClass (itmGetName gItem '(noEvent noModifiers)))
							(setq pluralClass (itmGetName gItem '(noEvent noModifiers plural)))
							)
						)
					(if (setq name (@ (itmGetData gItem 'autonConfig) 'name))
						(typTranslate &baAutonItemBase; 'nameText {
							autonName:(substitute name "\"" "'")
							singularClass:singularClass
							pluralClass:pluralClass
							})
						(cat singularClass "|" pluralClass)
						)
					)
			</GetName>
			
			<OnUpdate>
				(block (config)
					(if (and (setq config (itmGetData gItem 'autonConfig))
							(or (isError (scrGetScreen gScreen)) (not (scrGetScreen gScreen)))
							(@ config 'shieldStatus)
							(eq (@ (rpgGetAutonItemShieldStatus config) 'percent) 100)
							)
						(objSetItemData gSource gItem 'autonConfig (set@ config 'shieldStatus Nil) (itmGetCount gItem))
						)
					)
			</OnUpdate>
		</Events>
		
		<Language>
			<Text id="nameText">%singularClass% named "%autonName%"|%pluralClass% named "%autonName%"</Text>
		</Language>
	</ItemType>
	
	<!-- Auton Base Class
	
	Do not rely on the implementation of this class across versions. If you wish
	to inherit from this class in an Extension, you should copy the class into
	your own Extension and then inherit from that.

	EXTRA DATA
	
	autonItemType:		The type of item that created the auton.
	
	behavior:			Autons's current behavior
							Nil						= nothing
							'attackingAtWill		= attacking any target in range
							'attackingTarget		= attacking a target
							'custom					= handled by derived class
							'escorting				= escorting the player
							'returning				= returning to player ship
							'waiting				= waiting for the player

	EVENTS
		
	Derived types may implement the following events:
		
	CreateFromItem: Create the auton ship. gData has the following fields:
				
			item: The item used to create the auton
			sourceObj: The object launching the auton
			type: The ship class of the auton to create
				
			By default this event creates an auton of the given type and 
			orders it to escort the launching object.
				
			NOTE: If you override this event it recommended that you call
			the base class event (which returns the auton object) and then
			modify the auton.
				
	ConvertToItem: This event must return an item structure representing the
			auton when converted to an item. By default we look at the auton
			object's data ("autonItemType"), which is set by the default
			CreateFromItem event.
		
	OrderAttackSelf: Implement behavior when the auton is ordered to attack 
			itself. By default, the auton refuses to comply.
			
	LANGUAGE
	
	Derrived types may define the following language elements:
	
	AttackTargetAck: Reply when auton is ordered to attack a valid target.
	
	DeployAck: Reply when auton is deployed.
	
	FormUpAck: Reply when auton is ordered to form up.
	
	ReturnAck: Reply when auton is ordered to return to the ship.
	
	WaitAck: Reply when auton is ordered to wait.
				
	-->

	<Type UNID="&baStdAutonBase;">
		<Communications>
			<Message id="msgAttackTarget" name="Attack target" key="A">
				<OnShow>
					(and (objGetTarget gSender)
						(not (eq (objGetTarget gSender) (objGetTarget gSource)))
						(not (shpGetAISetting gSource 'nonCombatant))
						(objGetItems gSource "wI")
						(objGetProperty gSource 'playerWingman)
						)
				</OnShow>
				
				<Invoke>
					(switch
						;	If ordered to attack ourselves, hilarity ensues
						
						(eq (objGetTarget gSender) gSource)
							(objFireEvent gSource "OrderAttackSelf")
							
						;	If no target, unable to compy
						
						(not (objGetTarget gSender))
							(objSendMessageTranslate gSender gSource 'UnableToComply)

						;	Attack the target
						
						(block Nil
							(objSetData gSource 'behavior 'attackingTarget)
							(objSendMessageTranslate gSender gSource 'AttackTargetAck)
							
							(shpCancelOrders gSource)
							(shpOrder gSource 'attack (objGetTarget gSender))
							)
						)
				</Invoke>
			</Message>

			<Message id="msgBreakAndAttack" name="Break &amp; attack" key="B">
				<OnShow>
					(and (neq (objGetData gSource 'behavior) 'attackingAtWill)
						(not (shpGetAISetting gSource 'nonCombatant))
						(objGetItems gSource "wI")
						(objGetProperty gSource 'playerWingman)
						)
				</OnShow>
				
				<Invoke>
					(intOrderBreakAndAttack gSource gSender True)
				</Invoke>
			</Message>

			<Message id="msgFormUp" name="Form up" key="F">
				<OnShow>
					(and (neq (objGetData gSource 'behavior) 'escorting)
						(objGetProperty gSource 'playerWingman)
						)
				</OnShow>

				<Invoke>
					(block (behavior)
						(setq behavior (objGetData gSource 'behavior))
						(switch
							(or (not behavior) (eq behavior 'escorting))
								(block Nil
									(objCommunicate gSource gSender 'FormUp)
									(objSendMessageTranslate gSender gSource 'FormUpAck)
									)

							(block Nil
								(objSetData gSource 'behavior 'escorting)
								(objSendMessageTranslate gSender gSource 'FormUpAck)
								
								(shpCancelOrders gSource)
								(shpOrder gSource 'escort gPlayerShip)
								)
							)
						)
				</Invoke>
			</Message>
			
			<Message id="msgReturnToShip" name="Return to ship" key="R">
				<OnShow>
					(and
						(objGetItems gSender "*IN +AutonBay;")
						(objGetData gSource 'autonItemType)
						(neq (objGetData gSource 'behavior) 'returning)
						(objGetProperty gSource 'playerWingman)
						)
				</OnShow>
				
				<Invoke>
					(block (spaceLeft mass)
					
						;	Make sure there will be enough space to return, accounting for
						;	autons that are already returning. Search the system if we haven't
						;	done so yet this tick.
						
						(if (eq (unvGetTick) (objGetData gSender 'returningAutonMassCheckTick))
							(setq spaceLeft (objGetData gSender 'spaceLeftAfterAutonsReturn))
							(block nil
								(setq spaceLeft (objGetCargoSpaceLeft gSender))
								(enum (sysFindObject nil 's) theShip
									(if (and (objGetData theShip 'autonItemType)
											(eq (objGetData theShip 'behavior) 'returning)
											(eq (shpGetOrderTarget theShip) gSender)
											)
										(setq spaceLeft (- spaceLeft (objFireEvent theShip "CalcMass")))
										)
									)
								(objSetData gSender 'returningAutonMassCheckTick (unvGetTick))
								)
							)
						
						(setq mass (objFireEvent gSource "CalcMass"))
						(if (gr mass spaceLeft)
						
							;	Not enough space
							(objSendMessageTranslate gSender gSource 'ErrNoSpaceToReturn)
								
							;	Return
							(block Nil
								(objSetData gSource 'behavior 'returning)
								(objSendMessageTranslate gSender gSource 'ReturnAck)
								
								(setq spaceLeft (- spaceLeft mass))
								
								(shpCancelOrders gSource)
								(shpOrder gSource 'gate gSender)
								)
							)
							
						;	Remember remaining space
						(objSetData gSender 'spaceLeftAfterAutonsReturn spaceLeft)
						)
				</Invoke>
			</Message>

			<Message id="msgWait" name="Wait" key="W">
				<OnShow>
					(and (neq (objGetData gSource 'behavior) 'waiting)
						(objGetProperty gSource 'playerWingman)
						)
				</OnShow>

				<Invoke>
					(block Nil
						(objSetData gSource 'behavior 'waiting)
						(objSendMessageTranslate gSender gSource 'WaitAck)
						
						(shpCancelOrders gSource)
						(shpOrder gSource 'hold)
						)
				</Invoke>
			</Message>
		</Communications>
		
		<Events>
			<CalcMass>
				(block (autonType)
					(setq autonType (objGetData gSource 'autonItemType))
					(if (not autonType)
						(setq autonType &itAutoSentinel;)
						)

					(add
						(itmGetMass autonType)
						(map (objGetItems gSource "*U") 'reduceSum theItem 
							(multiply (itmGetCount theItem) (itmGetMass theItem))
							)
						)
					)
			</CalcMass>
			
			<ConvertToItem>
				(block (
					(autonType (or (objGetData gSource 'autonItemType) &itAutoSentinel;))
					(autonItem (itmCreate autonType 1))
					(shieldMaxHP (shpGetShieldMaxHitPoints gSource))
					(shieldDamage (shpGetShieldDamage gSource))
					)
						
					;	Store the current armor and devices on the auton
					
					(setq autonItem (itmSetData autonItem 'autonConfig {
						armor: (map (objGetItems gSource "aI") theArmor	{
							item: (itmSetProperty theArmor 'installed Nil)
							hp: (subtract (itmGetProperty theArmor 'completeHP) (objGetArmorDamage gSource theArmor))
							})
							
						devices: (map (objGetItems gSource "dI") theDev {
							item: (itmSetProperty theDev 'installed Nil)
							})
							
						shieldStatus: (if (gr shieldDamage 0) {
							maxHP: shieldMaxHP
							hp: (- shieldMaxHP shieldDamage)
							regen: (/ (typGetProperty (shpGetShieldItemUNID gSource) 'regen) 180)
							regenStartTick: (unvGetTick) ; TODO: Add a way to get depletion time left.
							})
							
						cargo: (objGetItems gSource "*U")
						
						name: (if (neq (objGetName gSource) (objGetProperty gSource 'name))
									(rpgSanitizeAutonName (objGetName gSource))
									)
						}))
						
					;	Add one charge on the item for every ton of cargo inside the auton
					
					(setq autonItem (itmSetProperty autonItem 'charges
						(divide 
							(map (objGetItems gSource "*U") 'reduceSum theItem 
								(multiply (itmGetCount theItem) (itmGetMass theItem))
								)
							1000
							)
						))
						
					;	Done
					
					autonItem
					)
			</ConvertToItem>
			
			<CreateFromItem>
				(block (auton sourceObj sourceItem autonConfig)
					(setq sourceObj (@ gData 'sourceObj))
					(setq sourceItem (@ gData 'item))
					
					;	Create the auton
					
					(setq auton
						(sysCreateShip 
							(@ gData 'type)
							(objGetPos sourceObj)
							(if (eq sourceObj gPlayerShip) &svFriendlyAuton; (objGetProperty sourceObj 'sovereign))
							)
						)
						
					;	If the source item has a configuration then we need to initialize the ship.
					
					(if (setq autonConfig (itmGetData sourceItem 'autonConfig))
						(block Nil
						
							;	Start by installing the proper armor
							
							(for i 0 (subtract (shpGetArmorCount auton) 1)
								(block (newArmorItem)
									(setq newArmorItem (@ (@ (@ autonConfig 'armor) i) 'item))
									
									(objAddItem auton newArmorItem)
									(shpInstallArmor auton newArmorItem i)
									
									;	Set the appropriate hit points
									
									(objSetItemProperty auton (shpGetArmor auton i) 'hp (@ (@ (@ autonConfig 'armor) i) 'hp))
									)
								)
								
							;	Uninstall all devices
							
							(enum (objGetItems auton "dI") theItem
								(shpRemoveDevice auton theItem)
								)
								
							;	Install required devices
							
							(enum (@ autonConfig 'devices) deviceDesc
								(block (newDeviceItem)
									(setq newDeviceItem (@ deviceDesc 'item))
									
									(objAddItem auton newDeviceItem)
									(shpInstallDevice auton newDeviceItem)
									)
								)
								
							;	Remove uninstalled armor and devices
								
							(enum (objGetItems auton "adU") theItem
								(objRemoveItem auton theItem)
								)
								
							;	Add items to cargo
							
							(enum (@ autonConfig 'cargo) theItem
								(objAddItem auton theItem)
								)
							
							;	Set name
							
							(if (@ autonConfig 'name)
								(objSetName auton (@ autonConfig 'name) 0xC0) ; Personal and no article
								)
								
							;	Set shield level
							
							(shpRechargeShield auton (or (@ (rpgGetAutonItemShieldStatus autonConfig) 'hp)
														(shpGetShieldMaxHitPoints auton)
								))
								
							)
						)
						
					;	Order the auton to escort the source
					
					(objSetData auton 'behavior 'escorting)
					(shpOrder auton 'escort sourceObj)
					
					;	Store the item type in the auton object so that we can
					;	convert it back later.
					
					(objSetData auton 'autonItem sourceItem)
					(objSetData auton 'autonItemType (itmGetType sourceItem))
					
					;	Welcome or error message
					
					(if (@ gData 'notEnoughSpace)
						(objSendMessageTranslate sourceObj auton 'ErrNoSpaceToReturn)
						(objSendMessageTranslate sourceObj auton 'DeployAck)
						)
					
					;	Return the auton created, in case subclasses want to
					;	modify it.
					
					auton
					)
			</CreateFromItem>
			
			<OnCreate>
				(block Nil
					;	If this is a friendly auton then we assume that we are escorting
					;	the player. We need to do this for backwards compatibility, in case
					;	anyone tries to create an auton without going through CreateFromItem
					
					(if (eq (objGetSovereign gSource) &svFriendlyAuton;)
						(block Nil
							(objSetProperty gSource 'playerWingman True)
							)
						)
					)
			</OnCreate>
			
			<OnEnteredGate>
				;	If we returned to a ship or non-gate station, convert back to an item
				
				(if (objMatches aGateObj nil "st -stargate;")
					(block ((autonItem (objFireEvent gSource "ConvertToItem"))
							(autonShipClass (itmGetStaticData autonItem 'autonShipClass))
							)
						(if (gr (itmGetMass autonItem) (objGetCargoSpaceLeft aGateObj))
						
							; If there's not enough space, redeploy with an error message
							(typFireEvent autonShipClass 'CreateFromItem
								{ sourceObj:aGateObj type:autonShipClass item:autonItem notEnoughSpace:true }
								)
							
							; Otherwise, add to cargo
							(objAddItem aGateObj autonItem)
							)
						)
					)
			</OnEnteredGate>
			
			<OnOrdersCompleted>
				(block Nil
					(switch
						(not (objGetProperty gSource 'playerWingman))
							Nil
						
						(not gPlayerShip)
							(block Nil
								(objSetData gSource 'behavior 'waiting)
								(shpOrder gSource 'hold)
								)
							
						(and (eq (objGetData gSource 'behavior) 'attackingAtWill)
							(intOrderBreakAndAttack gSource gPlayerShip Nil)
							)
							Nil
								
						;	Default: return to formation.
						(block Nil
							(objSetData gSource 'behavior 'escorting)
							(shpOrder gSource 'escort gPlayerShip)
							)
							
						)
					)
			</OnOrdersCompleted>
			
			<OnPlayerLeftSystem>
				(block (behavior)
					(setq behavior (objGetData gSource 'behavior))
					(switch
						(not (objGetProperty gSource 'playerWingman))
							Nil
							
						; If we're waiting, stay in this system and wait for the player
						
						(eq behavior 'waiting)
							'waitForPlayer

						; Otherwise, follow the player through the gate
						
						'followPlayer
						)
					)
			</OnPlayerLeftSystem>
			
			<OrderAttackSelf>
				(objSendMessageTranslate gSender gSource 'UnableToComply)
			</OrderAttackSelf>
		</Events>
		
		<Language>
			<String id="AttackTargetAck">"Auton order acknowledged"</String>
			<Text id="DeployAck">(objTranslate gSource 'DeployAckString { autonName:(objGetName auton 0x01) } )</Text>
			<String id="DeployAckString">"%autonName% online"</String>
			<String id="ErrNoSpaceToReturn">"Not enough space in cargo hold to return to ship"</String>
			<String id="FormUpAck">"Auton order acknowledged"</String>
			<String id="NiceShooting"></String>
			<String id="NoTargetInRange">"No target in range"</String>
			<String id="ReturnAck">"Auton order acknowledged"</String>
			<String id="UnableToComply">"Unable to comply"</String>
			<String id="WaitAck">"Auton order acknowledged"</String>
			<String id="WatchYourTargets"></String>
		</Language>
	</Type>
	
	<DockScreen UNID="&dsNameAuton;"
		nestedScreen= 	"true"
		>
		<Panes>
			<Default
				showTextInput=	"true"
			>
				<OnPaneInit>
					(scrSetDescTranslate gScreen 'descName 
						{ autonName:(itmGetName (typFireEvent (objGetType (@ gData 'autonObject)) 'ConvertToItem) 0) }
						)
				</OnPaneInit>

				<Actions>
					<Action id="actionName" default="1">
						(block ((theName (rpgSanitizeAutonName (scrGetInputText gScreen)))
								(theAuton (@ gData 'autonObject))
								)
							(objSetName theAuton (or theName (objGetProperty theAuton 'name)))
							(scrExitScreen gScreen)
							)
					</Action>
					
					<Action id="actionCancel" cancel="1">
						(scrExitScreen gScreen)
					</Action>
				</Actions>
			</Default>
		</Panes>
		
		<Language>
			<Text id="actionName">[Enter] Name</Text>
			<Text id="actionCancel">[Esc] Cancel</Text>
			<Text id="descName">What would you like to name this %autonName%?</Text>
		</Language>
	</DockScreen>

<!-- GLOBAL FUNCTIONS ==========================================================

-->

	<Globals>
		(block Nil
			(setq rpgCleanUpSuspendedAuton (lambda (sourceObj)
				(block (auton)
					(if (setq auton (objGetObjRefData sourceObj 'rpgSuspendedAuton))
						(objDestroy auton)
						)
						
					(objSetObjRefData sourceObj 'rpgSuspendedAuton Nil)
					)
				))

			(setq rpgCreateSuspendedAuton (lambda (sourceObj autonItem)
				(block (autonUNID auton)
					(switch
					
						;	Get the UNID
						
						(not (setq autonUNID (itmGetStaticData autonItem 'autonShipClass)))
							Nil
					
						;	Check to see if we've already created this auton. If so, then
						;	we just return it.
					
						(and (setq auton (objGetObjRefData sourceObj 'rpgSuspendedAuton))
								(itmIsEqual autonItem (objGetData auton 'autonItem))
								)
							auton
					
						;	Let the auton ship class create itself based on the item
					
						(block Nil
							(setq auton
								(typFireEvent autonUNID 'CreateFromItem { sourceObj:sourceObj type:autonUNID item:autonItem })
								)
								
							;	Suspend the ship
							
							(objSuspend auton)
								
							;	Remember the suspended auton, in case we need it later.
							
							(objSetObjRefData sourceObj 'rpgSuspendedAuton auton)
							
							;	Done
							
							auton
							)
						)
					)
				))
				
			(setq rpgDeployAutonFromItem (lambda (sourceObj autonUNID autonItem)
				(block Nil
					;	Let the auton ship class create itself based on the item
				
					(typFireEvent autonUNID 'CreateFromItem { sourceObj:sourceObj type:autonUNID item:autonItem })
				
					;	Identify and remove the item from the source
				
					(itmSetKnown autonItem)
					(objRemoveItem sourceObj autonItem 1)
					)
				))
				
			(setq rpgGetAutonConfig (lambda (autonItem)
				;
				;	We return a struct with the following elements:
				;
				;		armor: An array of structs, each struct with the following elements:
				;
				;				item: An item descriptor
				;				hp: The number of hit points left
				;
				;		default: If True, then the auton is identical to its original
				;				ship class.
				;
				;		devices: An array of structs, each struct with the following
				;				elements:
				;
				;				item: An item descriptor
				
				(block (savedConfig autonShipClass)
				
					(switch
						;	We've already stored the information, so we just return it.
						
						(setq savedConfig (itmGetData autonItem 'autonConfig))
							savedConfig
							
						;	If we have the ship class then we can take information directly
						;	from it.
						
						(setq autonShipClass (itmGetStaticData autonItem 'autonShipClass))
							{	default: True
							
								armor: (map (typGetDataField autonShipClass 'armorItems) Nil theArmor
									{
										item: theArmor
										hp: (itmGetProperty theArmor 'completeHP)
										}
									)

								devices: (map (typGetDataField autonShipClass 'deviceItems) Nil theDevice
									{
										item: theDevice
										}
									)
								}

						;	Otherwise, there is nothing we can do.

						Nil
						)
					)
				))
				
			(setq rpgSanitizeAutonName (lambda (theName)
				(block (tempName newName)
					;	Pairs of % signs with another character and no spaces between them could be parameters that will be replaced.
					;	We can't control the text parsing code or escape them, so break them up by inserting a space.
					
					(for i 0 (min 16 (- (count theName) 1))
						(if (and (eq (subset theName i 1) "%")
								(gr (strFind (subset theName (+ i 1)) "%") 0)
								(or (not (strFind (subset theName (+ i 1)) " "))
									(ls (strFind (subset theName (+ i 1)) "%") (strFind (subset theName (+ i 1)) " "))
									)
								)
							(setq tempName (cat tempName "% "))
							(setq tempName (cat tempName (subset theName i 1)))
							)
						)
					
					;	Escape characters with special function in item names with \
					
					(for i 0 (min 16 (- (count tempName) 1))
						(if (find "\\|[]();:" (subset tempName i 1))
							(setq newName (cat newName "\\" (subset tempName i 1)))
							(setq newName (cat newName (subset tempName i 1)))
							)
						)
				
					;	Replace empty string with Nil to reset to default name, otherwise return new name.
					
					(if (or (not newName) (eq "" (apply cat (split newName " ")))) Nil (strCapitalize newName))
					)
				))
			
			(setq rpgGetAutonItemShieldStatus (lambda (theConfig)
				(block (theStatus)
					(if (setq theStatus (@ theConfig 'shieldStatus))
						(block (
							(regenTime (- (unvGetTick) (@ theStatus 'regenStartTick) ))
							(hp (+ (@ theStatus 'hp) (* regenTime (@ theStatus 'regen))))
							(maxHP (@ theStatus 'maxHP))
							)
							(if (geq hp maxHP)
								(setq hp maxHP)
								(setq hp (int hp))
								)
							{
								hp: hp
								percent: (divide (* hp 100) maxHP)
								}
							)
							
						{ percent: 100 }
						)
					)
				))
			
			)
	</Globals>

</TranscendenceModule>
