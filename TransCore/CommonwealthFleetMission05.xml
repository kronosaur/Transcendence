<?xml version="1.0" ?>

<TranscendenceModule>

	<!-- Mission: Escort Freighter =============================================

	EXTRA DATA

	reward:			Reward (in credits) for completing mission
	missionXP:		XP awarded for completing mission
	freighterID:	The freighter to destroy
	gateID:			ID of the gate where freighter will arrive

	======================================================================== -->

	<MissionType UNID="&msCSCEscortFreighter;"
			name=			"Escort Freighter"
			attributes=		"commonwealthFleet, cscTaskForce, rank2"

			level=			"5-9"

			expireTime=			"5400"
			failureAfterOutOfSystem="5400"
			>

		<Events>
			<OnCreate>
				(block (gateList)

					(switch
						;	Find a stargate more than 300 light-seconds away
						(not (setq gateList (sysFindObject aOwnerObj "GR:300; -uncharted;")))
							(msnDestroy gSource)

						;	Create the mission
						(block Nil
							(msnSetData gSource 'gateID (objGetID (random gateList)))

							(msnSetData gSource 'missionXP 150)
							)
						)
					)
			</OnCreate>

			<OnStarted>
				(block (
					(cscObj (objGetObjByID (msnGetProperty gSource 'ownerID)))
					(gateObj (objGetObjByID (msnGetData gSource 'gateID)))
					(travelTime (sysCalcTravelTime (objGetDistance cscObj gateObj) (shpGetMaxSpeed gPlayerShip)))
					)
					;	Pick a random situation
					(msnSetData gSource 'variant (random (list
						'ares 'ares 'ares 'ares 'ares 'ares 'rogue 'rogue 'rogue 'attack
						)))

					(msnAddTimerEvent gSource (+ travelTime (random 100 400)) 'OnFreighterArrives)
					)
			</OnStarted>
			
			<OnObjDestroyed>
				(switch
					;	Freighter destroyed
					(= (objGetID aObjDestroyed) (msnGetData gSource 'freighterID))
						(if (= (msnGetData gSource 'status) 'kamikaze)
							(msnSuccess gSource)
							(msnFailure gSource)
							)
					)
			</OnObjDestroyed>

			<OnObjDocked>
				(switch
					(= (objGetID aObjDocked) (msnGetData gSource 'freighterID))
						(msnSuccess gSource)
					)
			</OnObjDocked>

			<OnFreighterArrives>
				(block (
					(cscObj (objGetObjByID (msnGetProperty gSource 'ownerID)))
					(gateObj (objGetObjByID (msnGetData gSource 'gateID)))
					(variant (msnGetData gSource 'variant))
					shipObj travelTime
					)
					(plyMessage gPlayer "Freighter arrived!")

					;	Create the superfreighter
					(setq shipObj (sysCreateShip &scScarabFreighter; gateObj &svCommonwealth;))
					(objSetData shipObj 'noPiracyCheck True)
					(objSetIdentified shipObj)

					(shpOrder shipObj 'dock cscObj)
					(shpOrder shipObj 'wait (random 6 18))
					(shpOrder shipObj 'gate)

					;	Register for events
					(msnSetData gSource 'freighterID (objGetID shipObj))
					(msnRegisterForEvents gSource shipObj)

					;	Update state and player target
					(msnSetData gSource 'state 'inbound)
					(msnSetPlayerTarget gSource)
					
					;	Calculate travel time for setting ambush
					(setq travelTime (sysCalcTravelTime (objGetDistance cscObj gateObj) (shpGetMaxSpeed shipObj)))
					;(dbgOutput "travel time: " travelTime)
					;	Variant specific setup
					(switch
						(= variant 'ares)
							(block Nil
								(sysAddEncounterEventAtDist 
									(random 1000 1500) 
									shipObj 
									&etAresAmbush4; 
									(random 120 200)
									)
								(if (leq (random 1 100) 90)
									(sysAddEncounterEventAtDist 
										(random 1000 1500) 
										gPlayerShip 
										&etAresAmbush2; 
										(random 120 200)
										)
									)
								(if (leq (random 1 100) 20)
									(sysAddEncounterEventAtDist 
										(random 1350 1900) 
										shipObj 
										&etAresAmbush2; 
										(random 120 200)
										)
									)
								)
						(= variant 'rogue)
							(block (
								(pathPos (random 35 65))
								ambushPos mineLayer waitPos
								)
								(setq ambushPos (sysGetNavPathPoint
									&svCommonwealth;
									gateObj
									cscObj
									pathPos
									))
								;	Create a rogue transport that will lay mines in the path
								(setq mineLayer (sysCreateShip &scRogueMineLayer; ambushPos &svRogueFleet;))
								(objSetData mineLayer 'MinePos ambushPos)
								
								;	Create a bunch of mines in addition
								(for i 0 (random 10 20)
									(sysCreateWeaponFire &itStaticMine; mineLayer (sysVectorPolarOffset ambushPos (random 0 359) (random 1 5)) 0 0 Nil)
									)

								;	Figure out where the centurions will wait
								(setq waitPos (sysGetNavPathPoint
									&svCommonwealth;
									gateObj
									cscObj
									(+ pathPos 15)
									))
								(setq waitPos (sysCreateMarker "" waitPos &svRogueFleet;))

								; Create some centurions that will attack once the freighter
								; goes through the mines
								(for i 0 (random 2 4)
									(block (theShip thePos)
										(setq thePos (sysVectorPolarOffset waitPos (random 0 359) (random 10 14)))
										(setq theShip (sysCreateShip &scCenturionRogue; thePos &svRogueFleet;))
										(shpOrderPatrol theShip waitPos 7)
										)
									)

								)
						(= variant 'attack)
							(block Nil
								(objAddItem shipObj (itmCreate &itHydrogenGas; 500))
								(msnAddRecurringTimerEvent gSource 31 'OnSuicideRunCheck)
								)
						)
					)
			</OnFreighterArrives>

			<OnSuicideRunCheck>
				(block (
					(cscObj (objGetObjByID (msnGetProperty gSource 'ownerID)))
					(shipObj (objGetObjByID (msnGetData gSource 'freighterID)))
					(dist (objGetDistance cscObj shipObj))
					)
					;(dbgOutput dist)
					(switch
						(not shipObj)
							(msnCancelTimerEvent gSource 'OnSuicideRunCheck)
						
						(ls dist 5)
							(block (
								(thePos (objGetPos shipObj))
								)
								(msnFailure gSource)
								(msnCancelTimerEvent gSource 'OnSuicideRunCheck)
								(objDestroy shipObj shipObj)
								(sysCreateWeaponFire &itM5Missile; Nil thePos 0 0 cscObj True)
								)

						(and (ls dist 40) (= (msnGetData gSource 'status) 'kamikaze))
							(block Nil
								(if (leq (random 1 100) 25)
									(objSendMessage gPlayerShip cscObj (msnTranslate gSource 'msgHurry))
									)
								)
						
						(and (ls dist 60) (!= (msnGetData gSource 'status) 'kamikaze))
							(block Nil
								;	Freighter is on sucide run
								(shpCancelOrders shipObj)
								(shpOrder shipObj 'goto cscObj)
								(shpOrder shipObj 'hold)
								
								;	Set the CSC to wait (so that it doesn't blast the freighter on its own)
								;	we assume that the CSC is caught by surprise
								;	Not needed as the freighter is still "friendly"
								;(shpCancelOrders cscObj)
								;(shpOrder cscObj 'wait 15)
								;(shpOrder cscObj 'hold)

								;	Player should destroy freighter
								(objSendMessage gPlayerShip cscObj (msnTranslate gSource 'msgKamikaze))

								(msnSetData gSource 'status 'kamikaze)
								(msnSetPlayerTarget gSource)
								)
						)
					)
			</OnSuicideRunCheck>

			<OnSetPlayerTarget>
				(switch
					(not (msnGetData gSource 'state))
						(rpgSetTarget gSource aReason (objGetObjByID (msnGetData gSource 'gateID)) 'dock)

					(= (msnGetData gSource 'state) 'kamikaze)
						(rpgSetTarget gSource aReason (objGetObjByID (msnGetData gSource 'freighterID)) 'attack)

					(rpgSetTarget gSource aReason (objGetObjByID (msnGetData gSource 'freighterID)) 'escort)
					)
			</OnSetPlayerTarget>

			<OnReward>
				(commFleet_GiveReward gSource)
			</OnReward>
		</Events>

		<Language>
			<Text id="Name">
				Escort freighter
			</Text>
			<Text id="Summary">
				(msnTranslate gSource 'textSummary {
					cscName: (objGetName (objGetObjByID (msnGetProperty gSource 'ownerID)) 'article)
					gateName: (objGetName (objGetObjByID (msnGetData gSource 'gateID)))
					systemName: (sysGetName)
					payment: (fmtCurrency 'credit (msnGetData gSource 'reward))
					})
			</Text>
			<Text id="textSummary">
				Escort a superfreighter carrying pteracnium ore from
				%gateName% back to %cscName%.

				System: %systemName%\n
				Payment: %payment%
			</Text>
			<Text id="Intro">

				The XO walks up to you.

				"Good to see you pilot. We've got a mission for you."

			</Text>
			<Text id="Briefing">
				(msnTranslate gSource 'textBriefing {
					cscName: (objGetName (objGetObjByID (msnGetProperty gSource 'ownerID)) 'article)
					gateName: (objGetName (objGetObjByID (msnGetData gSource 'gateID)))
					})
			</Text>
			<Text id="textBriefing">

				"A superfreighter carrying pteracnium ore will enter the system at
				%gateName% and rendezvous with us here. Your mission is to escort it
				safely from the gate to our location. Clear?"

			</Text>
			<String id="AcceptReply">
				"Good luck, %name%!"
			</String>
			<String id="DeclineReply">
				"Get the hell out of here then!"
			</String>

			<String id="InProgress">
				"What is your major malfunction!? Get back out there and complete your mission!"
			</String>
			<String id="FailureDebrief">
				"You screwed that mission all the way to Sol and back. You better hit the sims, pilot!"
			</String>
			<Text id="SuccessDebrief">
				(msnTranslate gSource (cat "SuccessDebrief:" (msnGetData gSource 'variant)))
			</Text>
			<String id="SuccessDebrief:ares">
				"Excellent mission! That pteracnium fuel is desperately needed!"
			</String>
			<String id="SuccessDebrief:rogue">
				"Excellent mission! That pteracnium fuel is desperately needed!"
			</String>
			<String id="SuccessDebrief:attack">
				"Excellent mission! Your quick reaction probably saved hundreds of lives on this ship!"
			</String>
			<Text id="msgHurry">
				(random (list
					"Destroy the freighter!"
					"Blast the bloody freighter!"
					"Do you copy? Destroy the freighter now!"
					"%name%, destroy the freighter!"
					"The freighter is on a suicide run! Destroy it!"
					))
			</Text>
			<Text id="msgKamikaze">
				"Freighter on suicide run! Destroy it now!"
			</Text>
		</Language>
	</MissionType>

</TranscendenceModule>
